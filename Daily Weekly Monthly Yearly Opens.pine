// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© sbtnc
// Created: 2020-01-11
// Last modified: 2024-11-19
// version 6.0

// @version=5
indicator("Daily Weekly Monthly Yearly Opens", "DWMY O", true, max_lines_count=500)


//--------------------------------------------------------------------
//#region                      Constants
//--------------------------------------------------------------------

int     HEAD_PADDING    = -2
int     HEAD_TRANSP     = 60
string  LABEL_SIZE      = size.small
string  LABEL_STYLE     = label.style_none
string  LINE_STYLE      = line.style_solid
int     LINE_WIDTH      = 1
int     OFFSET_PADDING  = 4
string  TAIL_STYLE      = line.style_dotted

//#endregion


//--------------------------------------------------------------------
//#region                        Inputs
//--------------------------------------------------------------------

string  tailsTooltip            = "Extends last opens on historical bars."
string  headsTooltip            = "Extends previous opens in the future."
string  discoverPricesTooltip   = "Discovers the opening prices from the intraday chart.\n\nYou may use this setting when there are discrepancies between the data from intraday and high time frames."
string  extendedHoursTooltip    = "Discovers the opening prices from the extended trading hours if available."

bool    isDailyEnabledInput     = input.bool    (true,          "Daily",                                inline="Daily",     group="Opens")
color   dailyColorInput         = input.color   (color.green,   "",                                     inline="Daily",     group="Opens")
int     dailyLookbackInput      = input.int     (1,             "", 1,                                  inline="Daily",     group="Opens")
bool    isWeeklyEnabledInput    = input.bool    (true,          "Weekly",                               inline="Weekly",    group="Opens")
color   weeklyColorInput        = input.color   (color.orange,  "",                                     inline="Weekly",    group="Opens")
int     weeklyLookbackInput     = input.int     (1,             "", 1,                                  inline="Weekly",    group="Opens")
bool    isMonthlyEnabledInput   = input.bool    (true,          "Monthly",                              inline="Monthly",   group="Opens")
color   monthlyColorInput       = input.color   (color.red,     "",                                     inline="Monthly",   group="Opens")
int     monthlyLookbackInput    = input.int     (1,             "", 1,                                  inline="Monthly",   group="Opens")
bool    isYearlyEnabledInput    = input.bool    (true,          "Yearly",                               inline="Yearly",    group="Opens")
color   yearlyColorInput        = input.color   (color.blue,    "",                                     inline="Yearly",    group="Opens")
int     yearlyLookbackInput     = input.int     (1,             "", 1,                                  inline="Yearly",    group="Opens")
int     rightOffsetInput        = input.int     (20,            "Offset", 1,                                                group="Style")
bool    areTailsEnabledInput    = input.bool    (false,         "Show Tails",       tailsTooltip,                           group="Style")
bool    areHeadsEnabledInput    = input.bool    (false,         "Show Projections", headsTooltip,                           group="Style")
bool    discoverPricesInput     = input.bool    (false,         "Discover Prices",  discoverPricesTooltip,                  group="Settings")
bool    extendedHoursInput      = input.bool    (false,         "Extended Hours",   extendedHoursTooltip,                   group="Settings")

//#endregion


//--------------------------------------------------------------------
//#region                 Variables declarations
//--------------------------------------------------------------------

var a_lastOpens             = array.new_float(4)
var canShowDaily            = isDailyEnabledInput and timeframe.isintraday
var canShowWeekly           = isWeeklyEnabledInput and (timeframe.isintraday or timeframe.isdaily)
var canShowMonthly          = isMonthlyEnabledInput and not timeframe.ismonthly
var canShowYearly           = isYearlyEnabledInput and not (timeframe.ismonthly and timeframe.multiplier >= 12)
var hasExtendedHours        = extendedHoursInput and syminfo.session == session.extended
var discoverPrices          = discoverPricesInput or hasExtendedHours

[dailyTime, dailyOpen]      = request.security(syminfo.tickerid, 'D',   [time, open], lookahead=barmerge.lookahead_on)
[weeklyTime, weeklyOpen]    = request.security(syminfo.tickerid, 'W',   [time, open], lookahead=barmerge.lookahead_on)
[monthlyTime, monthlyOpen]  = request.security(syminfo.tickerid, 'M',   [time, open], lookahead=barmerge.lookahead_on)
[yearlyTime, yearlyOpen]    = request.security(syminfo.tickerid, '12M', [time, open], lookahead=barmerge.lookahead_on)

//#endregion


//--------------------------------------------------------------------
//#region                   Functions & methods
//--------------------------------------------------------------------

// If different opens share the same opening prices, their labels will overlap
// Instead, we pad the higher time frame ones for achieving a "separator sheet"
f_getPadding(int _index) =>
    _padding = 0
    // Weekly, monthly, or yearly overlaps the daily open
    if _index > 0 and canShowDaily and array.get(a_lastOpens, 0) == array.get(a_lastOpens, _index)
        _padding += 1
    // Monthly or yearly overlaps the weekly open
    if _index > 1 and canShowWeekly and array.get(a_lastOpens, 1) == array.get(a_lastOpens, _index)
        _padding += 1
    // Yearly overlaps the monthly open
    if _index > 2 and canShowMonthly and array.get(a_lastOpens, 2) == array.get(a_lastOpens, _index)
        _padding += 1
    _padding

f_getRightBarIndex(int _padding) => bar_index + rightOffsetInput + _padding * OFFSET_PADDING

// On the weekly time frame, the monthly/yearly open is drawn from the first candle of the month/year
// The first weekly candle is not necessarily containing the first day of the month
// In such case, we visually anchor the open of the previous weekly candle
f_isContainedOnPrevWeekly(int _time) => timeframe.isweekly and dayofmonth(time) > dayofmonth(_time)

// Compare previous value and new value
// When the argument series contains `na` don't resolve `na` (returns true on the first bar's comparaison)
f_hasChanged(_series) => not (_series == _series[1])

// Create and update an open level. An open is composed of a label, a "body" line, and optional "tail" and "head" lines extensions
f_draw(bool _newOpen, float _y, int _lookback, int _padding, color _color, string _text, bool _prevTime=false) =>
    var line _body  = na 
    var _label      = label.new(na, na, _text, style=LABEL_STYLE, textcolor=_color, size=LABEL_SIZE)
    var _tail       = line.new(na, na, na, na, color=_color, style=TAIL_STYLE, width=LINE_WIDTH, extend=extend.left)
    var _bodies     = array.new_line()
    var _heads      = array.new_line()
    _start          = _prevTime ? bar_index - 1 : bar_index
    _end            = f_getRightBarIndex(_padding)

    if _newOpen
        // Label
        label.set_xy(_label, _end, _y)
        label.set_tooltip(_label, str.tostring(_y, format.mintick))

        // Body
        line.set_x2(_body, bar_index)
        _body := line.new(_start, _y, _end, _y, color=_color, style=LINE_STYLE, width=LINE_WIDTH)
        array.push(_bodies, _body)

        // Head
        if areHeadsEnabledInput
            array.push(_heads, line.new(bar_index, _y, bar_index, _y, color=color.new(_color, HEAD_TRANSP), style=LINE_STYLE, width=LINE_WIDTH))

        // Tail
        if areTailsEnabledInput
            line.set_xy1(_tail, _start - 1, _y)
            line.set_xy2(_tail, _start, _y)

        if array.size(_bodies) > _lookback
            line.delete(array.shift(_bodies))
        if array.size(_heads) > _lookback
            line.delete(array.shift(_heads))

    if barstate.islast
        line.set_x2(_body, _end)
        label.set_x(_label, _end)

        if areHeadsEnabledInput and array.size(_heads) > 1
            // Not updating the last open's projection
            for i = 0 to array.size(_heads) - 2
                // Avoid projecting on last opens bodies
                if not array.includes(a_lastOpens, line.get_y1(array.get(_heads, i)))
                    line.set_x2(array.get(_heads, i), f_getRightBarIndex(HEAD_PADDING))

//#endregion


//--------------------------------------------------------------------
//#region                         Logic
//--------------------------------------------------------------------

hasDailyTimeChanged     = hasExtendedHours ? ta.change(time_tradingday) : discoverPrices ? f_hasChanged(dailyTime) and time == dailyTime        : f_hasChanged(dailyTime)
hasWeekklyTimeChanged   = hasExtendedHours ? ta.change(weekofyear)      : discoverPrices ? f_hasChanged(weeklyTime) and time == weeklyTime      : f_hasChanged(weeklyTime)
hasMonthlyTimeChanged   = hasExtendedHours ? ta.change(month)           : discoverPrices ? f_hasChanged(monthlyTime) and time == monthlyTime    : f_hasChanged(monthlyTime)
hasYearlyTimeChanged    = hasExtendedHours ? ta.change(year)            : discoverPrices ? f_hasChanged(yearlyTime) and time == yearlyTime      : f_hasChanged(yearlyTime)

if canShowDaily and hasDailyTimeChanged 
    array.set(a_lastOpens, 0, discoverPrices ? open : dailyOpen)

if canShowWeekly and hasWeekklyTimeChanged
    array.set(a_lastOpens, 1, discoverPrices ? open : weeklyOpen)

if canShowMonthly and hasMonthlyTimeChanged
    array.set(a_lastOpens, 2, discoverPrices ? open : monthlyOpen)

if canShowYearly and hasYearlyTimeChanged
    array.set(a_lastOpens, 3, discoverPrices ? open : yearlyOpen)

//#endregion


//--------------------------------------------------------------------
//#region                        Visuals
//--------------------------------------------------------------------

if canShowYearly
    f_draw(hasYearlyTimeChanged, array.get(a_lastOpens, 3), yearlyLookbackInput, f_getPadding(3), yearlyColorInput, "Y   ", f_isContainedOnPrevWeekly(yearlyTime))

if canShowMonthly
    f_draw(hasMonthlyTimeChanged, array.get(a_lastOpens, 2), monthlyLookbackInput, f_getPadding(2), monthlyColorInput, "M   ", f_isContainedOnPrevWeekly(monthlyTime))

if canShowWeekly
    f_draw(hasWeekklyTimeChanged, array.get(a_lastOpens, 1), weeklyLookbackInput, f_getPadding(1), weeklyColorInput, "W   ")

if canShowDaily
    f_draw(hasDailyTimeChanged, array.get(a_lastOpens, 0), dailyLookbackInput, f_getPadding(0), dailyColorInput, "D   ")

// Plot invisible opens for displaying last values in `status line`, `scale`, `data window` as well for providing defaults alert conditions

plot(array.get(a_lastOpens, 0), "D",    color.new(dailyColorInput, 100),   editable=false)
plot(array.get(a_lastOpens, 1), "W",    color.new(weeklyColorInput, 100),  editable=false)
plot(array.get(a_lastOpens, 2), "M",    color.new(monthlyColorInput, 100), editable=false)
plot(array.get(a_lastOpens, 3), "Y",    color.new(yearlyColorInput, 100),  editable=false)

//#endregion