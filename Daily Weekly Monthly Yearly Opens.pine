// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© sbtnc
// Created: 2020-01-11
// Last modified: 2024-11-21
// version 6.0

// @version=5
indicator("Daily Weekly Monthly Yearly Opens", "Opens", overlay = true, max_lines_count = 500)


//--------------------------------------------------------------------
//#region                      Constants
//--------------------------------------------------------------------

int     HEAD_PADDING    = -2
int     HEAD_TRANSP     = 60
string  LABEL_SIZE      = size.small
string  LABEL_STYLE     = label.style_none
string  LINE_STYLE      = line.style_solid
int     LINE_WIDTH      = 1
int     OFFSET_PADDING  = 4
string  TAIL_STYLE      = line.style_dotted
color   COLOR_1         = color.green
color   COLOR_2         = color.orange
color   COLOR_3         = color.red
color   COLOR_4         = color.blue

//#endregion


//--------------------------------------------------------------------
//#region                         Types 
//--------------------------------------------------------------------

// @type 
type Open 
    float   price 
    int     barIndex
    string  name


// @enum Contains fields with the indicator configurable timeframes as titles.
enum Timeframes
    s5  = "5 seconds"
    s10 = "10 seconds"
    s15 = "15 seconds"
    s30 = "30 seconds"
    m1  = "1 minute"
    m2  = "2 minutes"
    m3  = "3 minutes"
    m5  = "5 minutes"
    m10 = "10 minutes"
    m15 = "15 minutes"
    m30 = "30 minutes"
    m45 = "45 minutes"
    h1  = "1 hour"
    h2  = "2 hours"
    h3  = "3 hours"
    h4  = "4 hours"
    h6  = "6 hours"
    h8  = "8 hours"
    h12 = "12 hours"
    D1  = "1 day"
    W1  = "1 week"
    M1  = "1 month"
    M3  = "3 months"
    M6  = "6 months"
    M12 = "12 months"

//#endregion


//--------------------------------------------------------------------
//#region                        Inputs
//--------------------------------------------------------------------

string      g1                          = "Opens"
string      g2                          = "Style"
string      g3                          = "Preferences"

string      tailsTooltip                = "Extends last opens on historical bars."
string      headsTooltip                = "Extends previous opens in the future."
string      discoverPricesTooltip       = "Discovers the opening prices from the intraday chart.\n\nYou may use this setting when there are discrepancies between the data from intraday and high time frames datafeeds."
string      extendedHoursTooltip        = "Display the opening prices on the extended trading hours if visible."

bool        enableOpenInput1            = input.bool        (true,              title = "", group = g1, inline = "#1")
Timeframes  openTimeframeInput1         = input.enum        (Timeframes.D1,     title = "", group = g1, inline = "#1", display = display.none)
color       openColorInput1             = input.color       (COLOR_1,           title = "", group = g1, inline = "#1")
int         openLookbackInput1          = input.int         (1,                 title = "", group = g1, inline = "#1", minval = 1, display = display.none)

bool        enableOpenInput2            = input.bool        (true,              title = "", group = g1, inline = "#2")
Timeframes  openTimeframeInput2         = input.enum        (Timeframes.W1,     title = "", group = g1, inline = "#2", display = display.none)
color       openColorInput2             = input.color       (COLOR_2,           title = "", group = g1, inline = "#2")
int         openLookbackInput2          = input.int         (1,                 title = "", group = g1, inline = "#2", minval = 1, display = display.none)

bool        enableOpenInput3            = input.bool        (true,              title = "", group = g1, inline = "#3")
Timeframes  openTimeframeInput3         = input.enum        (Timeframes.M1,     title = "", group = g1, inline = "#3", display = display.none)
color       openColorInput3             = input.color       (COLOR_3,           title = "", group = g1, inline = "#3")
int         openLookbackInput3          = input.int         (1,                 title = "", group = g1, inline = "#3", minval = 1, display = display.none)

bool        enableOpenInput4            = input.bool        (true,              title = "", group = g1, inline = "#4")
Timeframes  openTimeframeInput4         = input.enum        (Timeframes.M12,    title = "", group = g1, inline = "#4", display = display.none)
color       openColorInput4             = input.color       (COLOR_4,           title = "", group = g1, inline = "#4")
int         openLookbackInput4          = input.int         (1,                 title = "", group = g1, inline = "#4", minval = 1, display = display.none)

int         rightOffsetInput            = input.int         (20,                title = "Offset",           group = g2, minval = 1, display = display.none)
bool        enableTailsInput            = input.bool        (false,             title = "Show Tails",       group = g2, tooltip = tailsTooltip)
bool        enableHeadsInput            = input.bool        (false,             title = "Show Projections", group = g2, tooltip = headsTooltip)
bool        discoverPricesInput         = input.bool        (false,             title = "Discover Prices",  group = g3, tooltip = discoverPricesTooltip)
bool        enableExtendedSessionInput  = input.bool        (false,             title = "Extended Hours",   group = g3, tooltip = extendedHoursTooltip)

//#endregion


//--------------------------------------------------------------------
//#region                   Functions & methods
//--------------------------------------------------------------------

// @function Check if the specified open `timeframe` can be displayed on the chart context.
// @returns  simple bool
method isVisible(simple string timeframe) =>
    timeframe.in_seconds(timeframe) > timeframe.in_seconds()


// @function Check if the specified open change.
// @returns  series bool
method change(simple string timeframe, series int regularTime, simple bool enableExtended) =>
    switch enableExtended
        true  => timeframe.change(timeframe) // extended and HTF datafeed
        false => ta.change(regularTime) > 0


// @function 
// @returns  
method request(string timeframe, bool useExtendedTradingHours) =>
    string tickerId = ticker.modify(syminfo.tickerid, useExtendedTradingHours ? session.extended : session.regular)
    request.security(tickerId, timeframe, [time, open], lookahead = barmerge.lookahead_on)


// @function 
// @returns  
adjustStartBarIndex(int _time) =>
    time_close[1] > _time


// @function 
// @returns  
f_getRightBarIndex(int _padding) =>
    bar_index + rightOffsetInput + _padding * OFFSET_PADDING


// @function Produce the `timeframe` parameter from the given user input.
// @returns  string
method param(simple Timeframes input) =>
    switch input
        Timeframes.s5  => "5S"
        Timeframes.s10 => "10S"
        Timeframes.s15 => "15S"
        Timeframes.s30 => "30S"
        Timeframes.m1  => "1"
        Timeframes.m2  => "2"
        Timeframes.m3  => "3"
        Timeframes.m5  => "5"
        Timeframes.m10 => "10"
        Timeframes.m15 => "15"
        Timeframes.m30 => "30"
        Timeframes.m45 => "45"
        Timeframes.h1  => "60"
        Timeframes.h2  => "120"
        Timeframes.h3  => "180"
        Timeframes.h4  => "240"
        Timeframes.h6  => "360"
        Timeframes.h8  => "480"
        Timeframes.h12 => "720"
        Timeframes.D1  => "1D"
        Timeframes.W1  => "1W"
        Timeframes.M1  => "1M" 
        Timeframes.M3  => "3M"
        Timeframes.M6  => "6M"
        Timeframes.M12 => "12M"


// @function Return the short display string for minutes and hours `timeframe` string
// @returns  string
method displayString(string this) =>
    // Format
    _str = switch this
        "1"     => "m"
        "2"     => "2m"
        "3"     => "3m"
        "5"     => "5m"
        "10"    => "10m"
        "15"    => "15m"
        "30"    => "30m"
        "45"    => "45m"
        "60"    => "H"
        "120"   => "2H"
        "180"   => "3H"
        "240"   => "4H"
        "360"   => "6H"
        "480"   => "8H"
        "720"   => "12H"
        "1440"  => "24H"
        "1D"    => "D"
        "1W"    => "W"
        "1M"    => "M"
        "12M"   => "Y"
        => this

    // Add spacing
    switch str.length(_str)
        1 => _str + "   "
        2 => _str + "     "
        3 => _str + "       "


// @function Create and update an open level. An open is composed of a label, a "body" line, and optional "tail" and "head" lines extensions.
// @returns  void
draw(bool change, Open o, int lookback, int padding, color color, array<float> lastOpenArray) =>
    // @variable The reference of the last open line.
    var line _bodyLine = na
    // @variable The reference of the last open label.
    var label _label = label.new(x = na, y = na, text = o.name, style = LABEL_STYLE, textcolor = color, size = LABEL_SIZE)
    // @variable The reference of the last open left extending line.
    var line _tailLine = line.new(x1 = na, y1 = na, x2 = na, y2 = na, color = color, style = TAIL_STYLE, width = LINE_WIDTH, extend = extend.left)
    // @variable The collection of historical open lines.
    var array<line> _bodyLineArray  = array.new_line()
    // @variable The collection of historical right extending open lines.
    var array<line> _headLineArray  = array.new_line()

    int _endBarIndex    = f_getRightBarIndex(padding)

    if change
        // Anchor the old open position.
        _bodyLine.set_x2(o.barIndex)

        // Update the last open label position and tooltip.
        _label.set_xy(_endBarIndex, o.price)
        _label.set_tooltip(str.tostring(o.price, format.mintick))

        // Create the last open line.
        _bodyLine := line.new(x1 = o.barIndex, y1 = o.price, x2 = _endBarIndex, y2 = o.price, color = color, style = LINE_STYLE, width = LINE_WIDTH)
        array.push(_bodyLineArray, _bodyLine)

        // Create the last open right extension.
        if enableHeadsInput
            _headLineArray.push(line.new(x1 = o.barIndex, y1 = o.price, x2 = o.barIndex, y2 = o.price, color = color.new(color, HEAD_TRANSP), style = LINE_STYLE, width = LINE_WIDTH))

        // Create the last open left extension.
        if enableTailsInput
            _tailLine.set_xy1(o.barIndex -1, o.price)
            _tailLine.set_xy2(o.barIndex,    o.price)

        // Trim the exceeding number of drawings.
        if _bodyLineArray.size() > lookback
            line.delete(_bodyLineArray.shift())
        if _headLineArray.size() > lookback
            line.delete(_headLineArray.shift())

    if barstate.islast
        _bodyLine.set_x2(_endBarIndex)
        _label.set_x(_endBarIndex)

        if enableHeadsInput and _headLineArray.size() > 1
            // Not updating the last open's projection
            for i = 0 to _headLineArray.size() - 2
                // Avoid projecting on last opens bodies
                //if not lastOpenArray.includes(_headLineArray.get(i).get_y1())
                _headLineArray.get(i).set_x2(f_getRightBarIndex(HEAD_PADDING))


// @function 
// @returns 
// If different opens share the same opening prices, their labels will overlap
// Instead, we pad the higher time frame ones for achieving a "separator sheet"
f_getPadding(int _index, bool displayOpen1, bool displayOpen2, bool displayOpen3, array<Open> lastOpenArray) =>
    _padding = 0
    // Weekly, monthly, or yearly overlaps the daily open
    if _index > 0 and displayOpen1 and array.get(lastOpenArray, 0).price == array.get(lastOpenArray, _index).price
        _padding += 1
    // Monthly or yearly overlaps the weekly open
    if _index > 1 and displayOpen2 and array.get(lastOpenArray, 1).price == array.get(lastOpenArray, _index).price
        _padding += 1
    // Yearly overlaps the monthly open
    if _index > 2 and displayOpen3 and array.get(lastOpenArray, 2).price == array.get(lastOpenArray, _index).price
        _padding += 1
    _padding

//#endregion



//--------------------------------------------------------------------
//#region                 Variables declarations
//--------------------------------------------------------------------

// @variable True if using the open price of the extended session.
var bool useExtendedTradingHours = enableExtendedSessionInput and syminfo.session == session.extended


// @variable True if the open price is sourced from the chart context.
// Discover only if timeframe >= D ?
discoverPrice(timeframe) =>
    ((discoverPricesInput and timeframe.isintraday) or useExtendedTradingHours) and timeframe.in_seconds(timeframe) >= timeframe.in_seconds("D") 


// @variable The `timeframe` parameters produced from the user inputs.
var string openTimeframe1 = openTimeframeInput1.param()
var string openTimeframe2 = openTimeframeInput2.param()
var string openTimeframe3 = openTimeframeInput3.param()
var string openTimeframe4 = openTimeframeInput4.param()


// @variable Is true if the display setting is enabled and chart timeframe is lower than the open's one.
var bool displayOpen1 = enableOpenInput1 and openTimeframe1.isVisible()
var bool displayOpen2 = enableOpenInput2 and openTimeframe2.isVisible()
var bool displayOpen3 = enableOpenInput3 and openTimeframe3.isVisible()
var bool displayOpen4 = enableOpenInput4 and openTimeframe4.isVisible()


// @variable The opening time and price sourced from the user-selected timeframe context.
[time1, open1] = openTimeframe1.request(useExtendedTradingHours)
[time2, open2] = openTimeframe2.request(useExtendedTradingHours)
[time3, open3] = openTimeframe3.request(useExtendedTradingHours)
[time4, open4] = openTimeframe4.request(useExtendedTradingHours)


var Open o1 = Open.new(name = openTimeframe1.displayString())
var Open o2 = Open.new(name = openTimeframe2.displayString())
var Open o3 = Open.new(name = openTimeframe3.displayString())
var Open o4 = Open.new(name = openTimeframe4.displayString())
var openArray = array.from(o1, o2, o3, o4)

//#endregion


//--------------------------------------------------------------------
//#region                         Logic
//--------------------------------------------------------------------

bool openChange1 = openTimeframe1.change(time1, useExtendedTradingHours)
bool openChange2 = openTimeframe2.change(time2, useExtendedTradingHours)
bool openChange3 = openTimeframe3.change(time3, useExtendedTradingHours)
bool openChange4 = openTimeframe4.change(time4, useExtendedTradingHours)

a1 = adjustStartBarIndex(time1)
a2 = adjustStartBarIndex(time2)
a3 = adjustStartBarIndex(time3)
a4 = adjustStartBarIndex(time4)

if displayOpen1 and openChange1
    bool _discovery = discoverPrice(openTimeframe1)
    o1.price    := _discovery ? open : open1
    o1.barIndex := _discovery ? bar_index : a1 ? bar_index -1 : bar_index

if displayOpen2 and openChange2
    bool _discovery = discoverPrice(openTimeframe2)
    o2.price    := _discovery ? open : open2
    o2.barIndex := _discovery ? bar_index : a2 ? bar_index -1 : bar_index

if displayOpen3 and openChange3
    bool _discovery = discoverPrice(openTimeframe3)
    o3.price    := _discovery ? open : open3
    o3.barIndex := _discovery ? bar_index : a3 ? bar_index -1 : bar_index

if displayOpen4 and openChange4
    bool _discovery = discoverPrice(openTimeframe4)
    o4.price    := _discovery ? open : open4
    o4.barIndex := _discovery ? bar_index : a4 ? bar_index -1 : bar_index

//#endregion


//--------------------------------------------------------------------
//#region                        Visuals
//--------------------------------------------------------------------

if displayOpen4
    draw(
         openChange4,
         o4,
         openLookbackInput4,
         f_getPadding(3, displayOpen1, displayOpen2, displayOpen3, openArray),
         openColorInput4,
         na//lastOpenArray
         )

if displayOpen3
    draw(
         openChange3,
         o3,
         openLookbackInput3,
         f_getPadding(2, displayOpen1, displayOpen2, displayOpen3, openArray),
         openColorInput3,
         na//lastOpenArray
         )

if displayOpen2
    draw(
         openChange2,
         o2,
         openLookbackInput2,
         f_getPadding(1, displayOpen1, displayOpen2, displayOpen3, openArray),
         openColorInput2,
         na//lastOpenArray
         )

if displayOpen1
    draw(
         openChange1,
         o1,
         openLookbackInput1,
         f_getPadding(0, displayOpen1, displayOpen2, displayOpen3, openArray),
         openColorInput1,
         na//lastOpenArray
         )


// Plot the open prices on the `status line`, `scale`, `data window` as well for providing default alert conditions.

displayControls = display.status_line + display.price_scale + display.data_window

plot(series = o1.price, title = "#1", color = COLOR_1, editable = false, display = displayControls)
plot(series = o2.price, title = "#2", color = COLOR_2, editable = false, display = displayControls)
plot(series = o3.price, title = "#3", color = COLOR_3, editable = false, display = displayControls)
plot(series = o4.price, title = "#4", color = COLOR_4, editable = false, display = displayControls)

//#endregion


//--------------------------------------------------------------------
//#region                         Debug
//--------------------------------------------------------------------

var debugDashboard = table.new(position.bottom_right, 2, 5)

if barstate.isfirst
    debugDashboard.cell(0, 0, "extended",       text_color = chart.fg_color)
    debugDashboard.cell(0, 1, "discovery 1",    text_color = chart.fg_color)
    debugDashboard.cell(0, 2, "discovery 2",    text_color = chart.fg_color)
    debugDashboard.cell(0, 3, "discovery 3",    text_color = chart.fg_color)
    debugDashboard.cell(0, 4, "discovery 4",    text_color = chart.fg_color)

if barstate.islast
    debugDashboard.cell(1, 0, str.tostring(useExtendedTradingHours),        text_color = chart.fg_color)
    debugDashboard.cell(1, 1, str.tostring(discoverPrice(openTimeframe1)),  text_color = chart.fg_color)
    debugDashboard.cell(1, 2, str.tostring(discoverPrice(openTimeframe2)),  text_color = chart.fg_color)
    debugDashboard.cell(1, 3, str.tostring(discoverPrice(openTimeframe3)),  text_color = chart.fg_color)
    debugDashboard.cell(1, 4, str.tostring(discoverPrice(openTimeframe4)),  text_color = chart.fg_color)
 
//#endregion